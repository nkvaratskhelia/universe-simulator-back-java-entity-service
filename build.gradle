plugins {
    id 'java'
    id 'idea'
    id 'jacoco'
    id 'io.spring.dependency-management' version '1.0.12.RELEASE'
    id 'org.springframework.boot' version '2.7.1'
    id 'org.sonarqube' version '3.3'
    id 'org.owasp.dependencycheck' version '7.1.1'
}

group = 'com.example.universe.simulator'
version = '1.0.0'
sourceCompatibility = '17'

idea {
    module {
        downloadJavadoc = true
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

def coverageExclusions = [
        '**/com/example/universe/simulator/entityservice/EntityServiceApplication.*',
        '**/com/example/universe/simulator/entityservice/config/SchedulingConfig.*',
        '**/com/example/universe/simulator/entityservice/entities/*_.*',
        '**/com/example/universe/simulator/entityservice/mappers/*Impl.*'
]

jacocoTestReport {
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: coverageExclusions)
        }))
    }
    reports {
        xml.required = true
    }
}

jacocoTestCoverageVerification {
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: coverageExclusions)
        }))
    }
    violationRules {
        rule {
            limit {
                minimum = 1
            }
        }
    }
}

sonarqube {
    properties {
        property 'sonar.host.url', 'https://sonarcloud.io'
        property 'sonar.organization', 'universe-simulator'
        property 'sonar.login', System.getenv('US_SONARQUBE_TOKEN')
        property 'sonar.coverage.exclusions', coverageExclusions
    }
}

dependencyCheck {
    failBuildOnCVSS = 7
}

bootBuildImage {
    imageName = "universesimulator/${project.name}:${System.getenv('GITHUB_RUN_NUMBER')}"
    publish = true
    docker {
        publishRegistry {
            username = System.getenv('US_DOCKER_REGISTRY_USER')
            password = System.getenv('US_DOCKER_REGISTRY_TOKEN')
        }
    }
}

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        name = 'GitHubPackages'
        url = 'https://maven.pkg.github.com/nkvaratskhelia/universe-simulator-back-java-common'
        credentials {
            username = System.getenv('US_GITHUB_PACKAGES_USER')
            password = System.getenv('US_GITHUB_PACKAGES_TOKEN')
        }
        metadataSources {
            gradleMetadata()
        }
    }
}

ext {
    set('commonVersion', '1.4.3')
    set('modelmapperVersion', '3.1.0')
    set('logstashLogbackEncoderVersion', '7.2')
    set('springBootAdminVersion', '2.7.2')
    set('springdocOpenapiVersion', '1.6.9')
}

dependencyManagement {
    imports {
        mavenBom 'org.springframework.cloud:spring-cloud-dependencies:2021.0.3'
        mavenBom 'org.testcontainers:testcontainers-bom:1.17.3'
    }
}

dependencies {
    implementation "com.example.universe.simulator:common:${commonVersion}"
    implementation 'org.springframework.boot:spring-boot-starter-amqp'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation "org.modelmapper:modelmapper:${modelmapperVersion}"
    implementation "org.springdoc:springdoc-openapi-data-rest:${springdocOpenapiVersion}"
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'org.springframework.boot:spring-boot-starter-data-redis'
    runtimeOnly 'org.springframework.cloud:spring-cloud-starter-consul-config'
    runtimeOnly 'org.springframework.cloud:spring-cloud-starter-consul-discovery'
    runtimeOnly 'org.springframework.cloud:spring-cloud-starter-sleuth'
    // needed for spring cloud load balancer
    runtimeOnly 'com.github.ben-manes.caffeine:caffeine'
    runtimeOnly 'org.liquibase:liquibase-core'
    runtimeOnly "net.logstash.logback:logstash-logback-encoder:${logstashLogbackEncoderVersion}"
    runtimeOnly 'org.postgresql:postgresql'
    runtimeOnly "de.codecentric:spring-boot-admin-starter-client:${springBootAdminVersion}"
    runtimeOnly "org.springdoc:springdoc-openapi-ui:${springdocOpenapiVersion}"
    testImplementation 'org.springframework.boot:spring-boot-starter-data-redis'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.testcontainers:postgresql'
    testImplementation 'org.testcontainers:rabbitmq'
    // needed for smoke tests
    testRuntimeOnly 'com.h2database:h2'
    // adds ability to mock/verify final methods
    testRuntimeOnly 'org.mockito:mockito-inline'
    annotationProcessor 'org.hibernate:hibernate-jpamodelgen'
    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
}
