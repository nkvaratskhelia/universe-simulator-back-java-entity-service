plugins {
    id 'java'
    id 'idea'
    id 'jacoco'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'org.springframework.boot' version '2.5.4'
    id 'org.sonarqube' version '3.3'
    id 'org.owasp.dependencycheck' version '6.3.1'
}

group = 'com.example.universe.simulator'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

idea {
    module {
        downloadJavadoc = true
    }
}

test {
    useJUnitPlatform()
}

def coverageExclusions = [
        '**/com/example/universe/simulator/entityservice/EntityServiceApplication.*',
        '**/com/example/universe/simulator/entityservice/config/SchedulingConfig.*',
        '**/com/example/universe/simulator/entityservice/entities/*_.*',
        '**/com/example/universe/simulator/entityservice/properties/SchedulingProperties.*'
]

jacocoTestReport {
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: coverageExclusions)
        }))
    }
    reports {
        xml.required = true
    }
}

jacocoTestCoverageVerification {
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: coverageExclusions)
        }))
    }
    violationRules {
        rule {
            limit {
                minimum = 1
            }
        }
    }
}

sonarqube {
    properties {
        property 'sonar.host.url', 'https://sonarcloud.io'
        property 'sonar.organization', 'universe-simulator'
        property 'sonar.login', System.getenv('US_SONARQUBE_TOKEN')
        property 'sonar.coverage.exclusions', coverageExclusions
    }
}

dependencyCheck {
    failBuildOnCVSS = 7
}

bootBuildImage {
    imageName = "universesimulator/${project.name}"
    publish = true
    docker {
        publishRegistry {
            username = System.getenv('US_DOCKER_REGISTRY_USER')
            password = System.getenv('US_DOCKER_REGISTRY_PASSWORD')
        }
    }
}

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        name = 'GitHubPackages'
        url = System.getenv('US_GITHUB_PACKAGES_URL')
        credentials {
            username = System.getenv('US_GITHUB_PACKAGES_USER')
            password = System.getenv('US_GITHUB_PACKAGES_TOKEN')
        }
        metadataSources {
            gradleMetadata()
        }
    }
}

ext {
    set('springdocOpenapiVersion', '1.5.10')
}

dependencyManagement {
    imports {
        mavenBom 'org.springframework.cloud:spring-cloud-dependencies:2020.0.3'
        mavenBom 'org.testcontainers:testcontainers-bom:1.16.0'
    }
}

dependencies {
    implementation 'com.example.universe.simulator:common:0.1.5'
    implementation 'org.springframework.boot:spring-boot-starter-amqp'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.modelmapper:modelmapper:2.4.4'
    implementation "org.springdoc:springdoc-openapi-data-rest:${springdocOpenapiVersion}"
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'org.springframework.boot:spring-boot-starter-actuator'
    runtimeOnly 'org.springframework.boot:spring-boot-starter-validation'
    runtimeOnly 'org.springframework.cloud:spring-cloud-starter-bootstrap'
    runtimeOnly 'org.springframework.cloud:spring-cloud-starter-sleuth'
    runtimeOnly 'org.springframework.cloud:spring-cloud-starter-vault-config'
    runtimeOnly 'org.springframework.cloud:spring-cloud-sleuth-zipkin'
    runtimeOnly 'org.liquibase:liquibase-core'
    runtimeOnly 'org.postgresql:postgresql'
    runtimeOnly "org.springdoc:springdoc-openapi-ui:${springdocOpenapiVersion}"
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.testcontainers:postgresql'
    testImplementation 'org.testcontainers:rabbitmq'
    testRuntimeOnly 'com.h2database:h2'
    // adds ability to mock/verify final methods
    testRuntimeOnly 'org.mockito:mockito-inline'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.hibernate:hibernate-jpamodelgen'
    annotationProcessor 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
}
